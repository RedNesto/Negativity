import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'com.github.johnrengelman.shadow'
}

sourceSets {
    main {
        java.srcDirs = ['src']
        resources.srcDirs = ['resources']
    }
    test {
        java.srcDirs = ['testSrc']
        resources.srcDirs = ['testResources']
    }
}

dependencies {
    testImplementation("org.junit.jupiter:junit-jupiter-api:5.7.0")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.7.0")
    testRuntimeOnly("mysql:mysql-connector-java:8.0.25")
}

processResources {
    def props = [version: version]
    inputs.properties(props)
    // Do not try to expand service files, they may contain a dollar sign to mark inner classes
    filesNotMatching(['META-INF/services/*', 'assets/negativity/*.properties']) {
        expand(props)
    }
}

shadowJar {
    archiveBaseName.set('negativity')
    archiveAppendix.set('common')
    archiveClassifier.set(null)

    mergeServiceFiles()
    from(
            project('integrations:advancedban').jar.outputs.files,
            project('integrations:dkbans').jar.outputs.files,
            project('integrations:litebans').jar.outputs.files,
            project('integrations:viaversion').jar.outputs.files
    )
}

build.dependsOn shadowJar

parent.tasks.named('shadowJar', ShadowJar) {
    it.from(shadowJar.outputs)
}

test {
    useJUnitPlatform()

    workingDir(file('testRun'))
    doFirst {
        workingDir.mkdirs()
    }

    systemProperty 'negativity.testing', 'true'
    def testSystemProperties = [
            'negativity.mysql.url', 'negativity.mysql.user', 'negativity.mysql.password',
    ].collectEntries {
        def property = this.project.findProperty(it)
        if (property != null) [it, property] else []
    }
    testSystemProperties.remove(null) // Missing properties will add a null key and value, which isn't supported by Gradle.
    inputs.properties(testSystemProperties)
    systemProperties(testSystemProperties)
}

tasks.named('cleanTest', Delete.class) {
    it.delete(test.workingDir)
}
